plugins {
    id 'com.android.application'
    id 'dagger.hilt.android.plugin'
    id 'de.mannodermaus.android-junit5' version '1.8.2.1'
}

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdk globalConfiguration["compileSdk"]

    defaultConfig {
        applicationId "com.jorgediaz.emeals"
        minSdk globalConfiguration["minSdk"]
        targetSdk globalConfiguration["targetSdk"]

        versionCode globalConfiguration["versionCode"]
        versionName globalConfiguration["versionName"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            testCoverageEnabled true
        }
    }

    flavorDimensions "version", "target"
    productFlavors {
        staging {
            dimension "version"
            applicationIdSuffix ".staging"
            versionNameSuffix "-Staging"
            manifestPlaceholders = [scheme: "emeals.staging"]
            buildConfigField("String", "SCHEME", "\"${manifestPlaceholders["scheme"]}\"")
            buildConfigField("String", "BASE_URL", "\"https://emeals-menubuilder-public.s3.amazonaws.com/\"")
        }
        production {
            dimension "version"
            manifestPlaceholders = [scheme: "emeals.production"]
            buildConfigField("String", "SCHEME", "\"${manifestPlaceholders["scheme"]}\"")
            buildConfigField("String", "BASE_URL", "\"https://emeals-menubuilder-public.s3.amazonaws.com/\"")
        }
        internal {
            dimension "target"
        }
        external {
            dimension "target"
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        if (names.contains("external") && names.contains("staging")) {
            setIgnore(true)
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(':recipes-presentation')
    implementation project(':recipes-usecases')
    implementation project(':recipes-data')
    implementation project(':recipes-domain')

    def frameworkDependencies = rootProject.ext.frameworkDependencies
    def frameworkTestDependencies = rootProject.ext.frameworkTestDependencies

    implementation frameworkDependencies.multidex
    implementation frameworkDependencies.daggerHilt
    annotationProcessor frameworkDependencies.daggerHiltAndroidCompiler
    annotationProcessor frameworkDependencies.kotlinMetadata

    implementation frameworkDependencies.retrofit
    implementation frameworkDependencies.retrofitMoshi
    implementation frameworkDependencies.moshi
    annotationProcessor "com.github.nename0:moshi-java-codegen:1.1.1"
    implementation platform(frameworkDependencies.okHttpBoM)
    implementation frameworkDependencies.okHttp
    implementation frameworkDependencies.okHttpInterceptor

    implementation frameworkDependencies.timber

    implementation frameworkDependencies.retrofitRxJava

    implementation frameworkDependencies.room
    implementation frameworkDependencies.roomRxJava
    annotationProcessor frameworkDependencies.roomCompiler

    implementation frameworkDependencies.fresco

    testImplementation frameworkTestDependencies.jUnit
    testImplementation frameworkTestDependencies.jupiterApi
    testRuntimeOnly frameworkTestDependencies.jupiterEngine
    testImplementation frameworkTestDependencies.mockitoCore
}